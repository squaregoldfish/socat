! preview_xy_range
!  Version of LAS_is_range, called by preview map script. Handles
!  x and y; does not handle time 
!
! Given a min and max variables defined, see if they are valid data
! and use them to define axis min,max, delta. If min and max are not
! valid then return nominal 0:100 and set an error flag.

LET baddat = 0

CANCEL SYMBOL dm_labels

LET ax_lo = `$1`
LET ax_hi = `$2`
DEFINE SYMBOL ax_lo = `$1`
DEFINE SYMBOL ax_hi = `$2`

IF `($ax_lo"0|bad>1|*>0") OR ($ax_hi"0|bad>1|*>0")` THEN 
  LET ax_lo = 0
  LET amin = 0
  LET ax_hi = 100
  LET amax = 100
  LET del_ax = 100
  LET baddat = 1
  PPL axlabp,0,0
  PPL TICS,0,0,0,0
  DEFINE SYMBOL  put_label = 1
  EXIT/SCRIPT ! Data All Missing 
ENDIF 

IF `ax_lo EQ ax_hi` THEN
   LET ax_lo = `ax_lo - 0.5`
   LET ax_hi = `ax_hi + 0.5`  
ENDIF

PPL %RANGE `ax_lo`, `ax_hi`, 50
IF `($ppl$range_low) EQ ($ppl$range_high)` THEN
   LET del_ax = `(ax_hi - ax_lo)/10`
ELSE
   LET ax_lo = ($ppl$range_low)
   LET ax_hi = ($ppl$range_high)
   LET del_ax = ($ppl$range_inc)
ENDIF

IF `ABS(ax_hi - ax_lo) LT 1` THEN DEFINE SYMBOL dm_labels = dm
IF `ABS(ax_hi - ax_lo) LT 0.06` THEN DEFINE SYMBOL dm_labels = dms

! End of file ------------ preview_xy_range.jnl--------------------------
